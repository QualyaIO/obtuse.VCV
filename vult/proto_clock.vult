
// testing a tonnetz implementation using VCV.Prototype. Cannot use external files, so we will code here instead

/*

 getKnob(n:int) : real // value of the nth knob range: 0.0-1.0
 getSwitch(n:int) : bool // value of the nth switch: true/false

 setLight(n:int, r:real, g:real, b:real) // r, g, b range: 0.0-1.0, i from 1 to 6
 setSwitchLight(n:int, r:real, g:real, b:real) // r, g, b range: 0.0-1.0

 samplerate() : real  // current sample rate
 sampletime() : real  // current time step (1.0 / samplerate())

 */



// clock with swing and grouping


// - . o . - . o . normal
// - o . . - o . .  swing -
// - . . o - . . o swing +
// - . - . - . - . o . o . o . o . normal groupe 4 ... ou 8 en fait. swing pour min groupe 2. ratio 0.5
// - - - - o . . o . . o . . o . .  swing -


// inform the whole world about supported BPM and group range
fun getMinBPM(): real {
  return 0.002;
}
fun getMaxBPM(): real {
  return 30000.0;
}
// below 2 would probably crash something (e.g. division by 0 in IBI computation of clock)
fun getMinGroupSize(): int {
  return 2;
}
fun getMaxGroupSize(): int {
  return 128;
}


// time: passing by, in seconds, should be positive
// return 1 upon first beat, 2 new beat from first sub-group, 3 for second sub-group (groupped by IBI length afterward), 0 otherwise
// will return trigger upon first call (start with beat)
fun clock(time: real): int {
  // check if first run (or if reset), since vult init to false
  mem init: bool;
  // beats per minutes
  mem bpm: real;
  // interval between beats, in seconds, for both sub-groups
  mem ibiA: real;
  mem ibiB: real;
  // interval for next beat
  mem ibi: real;
  // when last beat occurred
  mem lastBeat: real;
  // how many beats to consider for swing.
  mem groupSize: int;
  // proportion belonging to first sub-group
  mem groupRatio: real;
  // size for first subgroup
  mem subSize: int;
  // where we are in the group
  mem pos: int; 
  // tune IBI, within sub-groups
  mem swing: real;
  //should be mix subgroups, even beat subgroup 1, odd subgroup 2
  mem orderMix: bool;

  val trigger = 0;
  // special case, we overflowed the time, or clock is wrong, consider that we should trigger, reset position
  if (not(init) || time < lastBeat) {
    init = true;
    lastBeat = time; 
    trigger = 1;
    pos = 1;
    // starting with group A
    ibi = ibiA;
  }
  // time to clock
  else if (time - lastBeat >= ibi) {
    // update time
    lastBeat = lastBeat + ibi;

    if (
        (not(orderMix) && pos < subSize) ||
        // in mix order, alternate between group A and B as long as there is enough group A left
        (orderMix && ((pos/2 < subSize && pos % 2 == 0)  || (pos/2 > groupSize - subSize)) )
        ) {
      // update interval to come
      ibi = ibiA;
      // can accentuate first beat
      if (pos == 0) {
        trigger = 1;
      } else {
        trigger = 2;
      }
    }
    else {
      ibi = ibiB;
      trigger = 3;
    }
    pos = pos + 1;
    pos = pos % groupSize;
  }
  return trigger;
}

// reset position in group, trigger on next call
and reset() {
  init = false;
}

// refresh groups and IBI for both groups
and _recompute() {
  // at least one beat in each group (groupSize + 1 for rounding)
  subSize = clip(int(real(groupSize + 1) * groupRatio), 1, groupSize - 1); 
  // base IBI
  val bibi = 60.0/bpm;
  // compress first sub-group
  if (swing <= 0.5) {
    // 2ms minimum IBI
    ibiA = clip(bibi * (swing * 2.0), 0.002, bibi);
    // split IBI for the second sub-group
    ibiB = (real(groupSize) * bibi - ibiA * real(subSize)) / real((groupSize - subSize));
  }
  // same algo there
  else {
    ibiB = clip(bibi * ((1.0 - swing) * 2.0), 0.002, bibi);
    ibiA = (real(groupSize) * bibi - ibiB * real(groupSize - subSize)) / real(subSize);
  }
}

// change current BPM, 0.002 to max 30000 bpm (to deal with fixed float range, and because why not)
and setBPM(newBPM: real) {
  newBPM = clip(newBPM, getMinBPM(), getMaxBPM());
  if (newBPM <> bpm){
    bpm = newBPM;
    _ = _recompute();
  }
}

// set how many beats to consider for swing. Typical swing is size 4 (I would say, for 4/4 signature, with a 2 divider) Clip from 2 (one beat in each sub-group) to 128. Can reset group position if we overflowed new group size.
and setGroupSize(newGroupSize: int) {
  newGroupSize = clip(newGroupSize, getMinGroupSize(), getMaxGroupSize());
  if (newGroupSize <> groupSize) {
    groupSize = newGroupSize;
    _recompute();
    // in case went already too far this group size
    pos = pos % groupSize;
  }
}

// how to divide the group, 0: one beat for first, rest for second, 0.5: half/half, 1: all but one for first, 1 for second
and setGroupRatio(newGroupRatio: real) {
  newGroupRatio = clip(newGroupRatio, 0.0, 1.0);
  if (newGroupRatio <> groupRatio) {
    groupRatio = newGroupRatio;
    _ = _recompute();
  }
}
// swing ratio, 0: min IBI (1ms) for first sub-group, 1: min for second sub-group. 0.5: neutral. min IBI: 1ms
and setSwing(newSwing: real) {
  newSwing = clip(newSwing, 0.0, 1.0);
  if (swing <> newSwing) {
    swing = newSwing;
    _ = _recompute();
  }
}

// change sub group order, from all 1, all 2 to 1 on even beats an 2 on odd beats
and setOrderMix(flag: bool) {
  orderMix = flag;
}

and default() @[init] {
  // good ol' 120 BPM
  _ = setBPM(120.0);
  // classical swing for 4/4, combined with divider 2 to get on second beat
  _ = setGroupSize(4);
  _ = setGroupRatio(0.5);
  // no swing at first
  _ = setSwing(0.5);
}

// creating 1ms gate from trigger and current time
fun gate1ms(trig: bool, time: real): bool {
  mem lastTrigg;
  mem gate;
  if (trig) {
    lastTrigg = time;
    gate = true;
  }
  else if (time - lastTrigg > 0.001) {
    gate = false;
  }
  return gate;
}

// no cast like that already?
fun bool2real(b: bool): real {
  if (b) {
    return 1.0;
  }
  else {
    return 0.0;
  }
}

// intput and output (up to 6) normalized from -10/10v to -1/1
// out1: trigger
// out2: trigger first beat
// out3: trigger first sub-group
// out4: trigger second sub-group
fun process() {
  // computing wall clock
  mem time;
  time = time + sampletime();
  // saving current state
  val out1, out2, out3, out4;

  val beat = cloclo:clock(time);
  out1 = bool2real(gate1ms(beat > 0, time));
  out2 = bool2real(gate1ms(beat == 1, time));
  out3 = bool2real(gate1ms(beat == 1 || beat == 2, time));
  out4 = bool2real(gate1ms(beat == 3, time));

  return out1, out2, out3, out4;
}

// called once every 32 loops
// knob1: BPM
// knob2: swing
// knob3: group size
// knob4: group ratio
// knob5: group order (from all A and all B to  mix A B A B)
and update() {
  // first knob: BPM
  val bpm = clip(getKnob(1) * 1000.0, getMinBPM(), getMaxBPM());
  _ = cloclo:setBPM(bpm);
  // second knob: swing
  _ = cloclo:setSwing(getKnob(2));
  // third knob: group size
  _ = cloclo:setGroupSize(int(getKnob(3) * real(getMaxGroupSize())));
  // fourth knob: group ratio
  _ = cloclo:setGroupRatio(getKnob(4));
  // fifth knob: group order
  _ = cloclo:setOrderMix(getKnob(5) > 0.5);
  
  // hack to display several things alternatively
  mem disstep;
  disstep = disstep + 1;
  val disfreq = 4000;

  if (disstep % disfreq < disfreq / 2) {
    _ = display("Clock. knob 1 BPM, 2 swing, 3 group size, 4 group ratio, 5 group order");
  } else {
    _ = display(string(bpm));
  }

}

