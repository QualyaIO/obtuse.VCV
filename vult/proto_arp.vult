
// testing a tonnetz implementation using VCV.Prototype. Cannot use external files, so we will code here instead

/*

 getKnob(n:int) : real // value of the nth knob range: 0.0-1.0
 getSwitch(n:int) : bool // value of the nth switch: true/false

 setLight(n:int, r:real, g:real, b:real) // r, g, b range: 0.0-1.0, i from 1 to 6
 setSwitchLight(n:int, r:real, g:real, b:real) // r, g, b range: 0.0-1.0

 samplerate() : real  // current sample rate
 sampletime() : real  // current time step (1.0 / samplerate())

 */


// from a midi note to cv -1..1
fun pitchToCv(pitch) {
    return (pitch - 60.0) / 120.0;
}

// and reciprocate
fun cvToPitch(cv) : int {
   val pitch = cv * 120.0 + 60.0;
   // round
   if (pitch % 1.0 >= 0.5) {
      pitch = floor(pitch + 1.0);
   } else {
      pitch = floor(pitch);
   }
   pitch = clip(pitch, 0.0, 127.0);
   return int(pitch);
}

// from vult examples
fun edge(x:bool) : bool {
   mem pre;
   val ret = x && not(pre);
   pre = x;
   return ret;
}

// for debug, name of mode
fun getModeName(mode) {
  if (mode == 0) {
    return "up";
  }
  else if (mode == 1) {
    return "down";
  }
  else if (mode == 2) {
    return "up-down";
  }
  else if (mode == 3) {
    return "up-down repeat";
  }
  else if (mode == 4) {
    return "down-up";
  }
  else if (mode == 5) {
    return "down-up repeat";
  }
  return "mode unknown";
}

// arpeggiator

// reset sequence to beginning
fun reset() {
  // position in current sequence
  mem step;
  step = 0;
}
// main function, return new note, -1 if none
and arp(): int {
  // up to 16 notes in the arp. 0..127, -1 if inactive
  mem notes: array(int, 16);
  // how many active notes
  mem nbNotes: int;
  // type of arp (see setmode)
  mem mode: int;
  // notes index in the sequence (size of the sequence max)
  mem sequence: array(int, 32);
  // size of current sequence
  mem sequenceSize;
  val newNote = notes[sequence[step]];
  step = step + 1;
  // reset at the end of the sequence
  if (step >= sequenceSize) {
    _ = reset();
  }
  return newNote;
}


// recompute sequence array
and _updateSequence() {
  // update number of active step in the sequence
  if (mode == 0 || mode == 1) {
    sequenceSize = nbNotes; 
  }
  else if (mode == 2 || mode == 4) {
    sequenceSize = nbNotes * 2 - 2; 
  }
  else if (mode == 3 || mode == 5) {
    sequenceSize = nbNotes * 2; 
  }
  // update sequence
  // TODO: probably more concise way to do it
  val i = 0;
  while (i < sequenceSize) {
    // up
    if (mode == 0) {
      sequence[i] = i;
    }
    // down
    else if (mode == 1) {
      sequence[i] = nbNotes - 1 - i;
    }
    // up-down no repeat
    else if (mode == 2) {
      if (i < nbNotes) {
        sequence[i] = i;
      }
      else {
        sequence[i] = nbNotes - 2 - (i - nbNotes);
      }
    }
    // up-down repeat
    else if (mode == 3) {
      if (i < nbNotes) {
        sequence[i] = i;
      }
      else {
        sequence[i] = nbNotes - 1 - (i - nbNotes);
      }
    }
    // down-up no repeat
    else if (mode == 4) {
      if (i < nbNotes) {
        sequence[i] = nbNotes - 1 - i;
      }
      else {
        sequence[i] = (i - nbNotes) + 1;
      }
    }
    // up-down repeat
    else if (mode == 5) {
      if (i < nbNotes) {
        sequence[i] = nbNotes - 1 - i;
      }
      else {
        sequence[i] = i - nbNotes;
      }
    }
    i = i + 1;
  }
  // re-init sequence
  _ = reset();
}

// updates notes of the arp, expect increasing order of pitch 0..127. Notes below 0 will not be considered, above 127 will be clipped.
// will reset sequence if the number of notes changes, otherwise left untouched
and setNotes(newNotes: array(int, 16)) {
  val i = 0;
  val j = 0;
  while (i < size(newNotes)){
    if (newNotes[i] > 0) {
      notes[j] = clip(newNotes[i], 0, 127);
      j = j + 1;
    }
    i = i + 1;
  }
  // check if the number of active notes changed
  if (j <> nbNotes) {
    nbNotes = j;
    _ = _updateSequence();
  }
}

// changing type of sequence -- resetting sequence if changed
// 0: up (1 2 3 1 2 3), sequence size nbNotes
// 1: down (3 2 1 3 2 1), sequence size nbNotes
// 2: up-down no repeat (1 2 3 2 1 2 3 2 1), sequence size nbNotes * 2 - 2
// 3: up-down repeat (1 2 3 3 2 1 1 2 3), sequence size nbNotes * 2
// 4: down-up no repeat (3 2 1 2 3 2 1), sequence size nbNotes * 2 - 2
// 5: down-up repeat (3 2 1 1 2 3 3 2 1), sequence size nbNotes * 2
and setMode(newMode: int) {
  newMode = clip(newMode, 0, 5);
  if (newMode <> mode) {
    mode = newMode;
    _ = _updateSequence();
  }
}

// get number of modes
and getNbModes() : int {
  return 6;
}

// intput and output (up to 6) normalized from -10/10v to -1/1
// Note: since we have no way to know if input is connected or not, a knob will be used to set the number of active inputs
// inputs notes from 1 to 4, 5 to trigger next note, 6 trigger to reset sequence
fun process(i1: real, i2: real, i3: real, i4: real, i5: real, i6: real) {
  // the one output
  mem out1;
  // how many active inputs, current and update
  mem nbInputs;
  mem newNbInputs;
  // current notes, 16 as handled by arp for simplification sake
  mem notes: array(int, 16);
  // candidates for new notes, first 4 inputs
  mem inputNotes: array(int, 4);
  // checking last values
  mem li1, li2, li3, li4;
  if (nbInputs <> newNbInputs || li1 <> i1 || li2 <> i2 || li3 <> i3 || li4 <> i4) {
    nbInputs = newNbInputs;
    li1 = i1;
    li2 = i2;
    li3 = i3;
    li4 = i4;
    inputNotes = [cvToPitch(i1), cvToPitch(i2), cvToPitch(i3), cvToPitch(i4)];
    // updating notes
    val i = 0;
    while (i < nbInputs) {
      notes[i] = inputNotes[i];
      i = i + 1;
    }
    // disable remaining notes (notes from max input to 16 already set to -1 on init)
    while (i < size(inputNotes)) {
      notes[i] = -1;
      i = i + 1;
    }
    // pass along
    _ = arpe:setNotes(notes);
  }
  // check reset, if last input => 1v (we are only interested in raising front)
  if (edge(i6 >= 0.1)) {
    _ = arpe:reset();
  }
  // update output
  if (edge(i5 >= 0.1)) {
    out1 = pitchToCv(real(arpe:arp()));
  }
  return out1;
}

// called once every 32 loops
and update() {
  // update inputs, at least one
  newNbInputs = int(getKnob(1) * real(size(inputNotes) - 1) + 0.5) + 1;
  // update mode
  val mode = int(getKnob(2) * real(arpe:getNbModes() - 1) + 0.5);
  _ = arpe:setMode(mode);

  // hack to display several things alternatively
  mem disstep;
  disstep = disstep + 1;
  val disfreq = 4000;
  if (disstep % disfreq < disfreq / 2) {
    _ = display("Arp. input 1-4 notes, 5 trigger, 6 reset. Knob 1 number inputs, 2 mode,  out 1 note.");
    _ = display("knobs: scale, root, chord, chordSpread, inversionSpread, jump");
  } else {
    _ = display(getModeName(mode));
  }
}

and default() @[init] {
  // init notes to all disabled
  val i = 0;
  while (i < size(notes)) {
    notes[i] = -1;
    i = i + 1;
  }
}
